plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
    id 'org.sonarqube' version '3.3'
    id 'com.adarshr.test-logger' version '3.0.0'
}

group 'com.ueby'
version '2.5-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

jar {
    manifest {
        attributes("Implementation-Title": "UEBY-LOTERIA-CRAWLERS",
                "Implementation-Version": archiveVersion)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Apache Fluent
    implementation group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.+'

    // Jackson
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.+'
    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-parameter-names', version: '2.9.+'
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: '2.9.+'
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.9.+'

    implementation group: 'org.jsoup', name: 'jsoup', version: '1.13.+'

    // Log4j
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.17.+'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.17.+'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.+'
    annotationProcessor 'org.projectlombok:lombok:1.18.+'

    testCompileOnly 'org.projectlombok:lombok:1.18.+'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.+'

    // JUnit
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.6.+")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.6.+")
}

javadoc {
    title 'UEBY LOTERIA CRAWLERS API'
    options.encoding = "ISO-8859-1"
    options.author = true
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/algarves/loteria-crawlers")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

tasks.withType(Test) {
    description = 'Runs the unit tests.'
    group = 'validation'
    useJUnitPlatform({
        excludeTags 'integrationTest'
    })
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    useJUnitPlatform {
        includeTags 'integrationTest'
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

apply from: "$project.rootDir/sonar.gradle"
