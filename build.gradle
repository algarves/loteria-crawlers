plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
}

group 'com.ueby'
version '2.2-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

jar {
    manifest {
        attributes("Implementation-Title": "UEBY-LOTERIA-CRAWLERS",
                "Implementation-Version": archiveVersion)
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    // Apache Fluent
    compile group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.+'

    // Jackson
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.+'
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-parameter-names', version: '2.9.+'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: '2.9.+'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.9.+'

    compile group: 'org.jsoup', name: 'jsoup', version: '1.13.+'

    // Log4j
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.13.+'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.13.+'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.+'
    annotationProcessor 'org.projectlombok:lombok:1.18.+'

    testCompileOnly 'org.projectlombok:lombok:1.18.+'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.+'

    // JUnit
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.6.+")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.6.+")
}

javadoc {
    title 'UEBY LOTERIA CRAWLERS API'
    options.encoding = "ISO-8859-1"
    options.author = true
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/algarves/loteria-crawlers")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

test {
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.2"
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

wrapper {
    gradleVersion = '6.3'
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-bin.zip"
}